class Tank
{
    static Array x_l, x_r, y_l, y_r, op;
    static Map map;

    field Image img;
    field int off_x, off_y;
    field int x, y, dir, type;
    field int live;

    constructor Tank new(int _type, int _x, int _y, int _dir)
    {
        let type = _type;
        let x = _x*3;
        let y = _y*3;
        let off_x = x*8;
        let off_y = y*8;
        let dir = _dir;
        let live = 5;
        let img = Album.get_tank(type);
        return this;
    }

	method void set_live(int _live){
		let live = _live;
		return;
	}
    function void init()
    {
        let x_l = Array.new(4);
        let x_r = Array.new(4);
        let y_l = Array.new(4);
        let y_r = Array.new(4);
        let op = Array.new(4);
        let op[0] = 2;
        let op[1] = 3;
        let op[2] = 0;
        let op[3] = 1;
        let x_l[0] = 0;
        let x_r[0] = 2;
        let y_l[0] = -1;
        let y_r[0] = -1;
        let x_l[1] = 3;
        let x_r[1] = 3;
        let y_l[1] = 0;
        let y_r[1] = 2;
        let x_l[2] = 0;
        let x_r[2] = 2;
        let y_l[2] = 3;
        let y_r[2] = 3;
        let x_l[3] = -1;
        let x_r[3] = -1;
        let y_l[3] = 0;
        let y_r[3] = 2;
        return;
    }
    function void set_map(Map _map)
    {
        let map = _map;
        return;
    }

	method void shoot(){
		var Bullet bullet;
		if (dir = 0){
			let bullet = Bullet.new(x+1, y, dir, type);
		}
		if (dir = 1){
			let bullet = Bullet.new(x+2, y+1, dir, type);
		}
		if (dir = 2){
			let bullet = Bullet.new(x+1, y+2, dir, type);
		}
		if (dir = 3){
			let bullet = Bullet.new(x, y+1, dir, type);
		}
		do Game.add_bullet(bullet);
		return;
	}
    method boolean move(int _dir)
    {
        let dir = _dir;
        if(~map.check(x+x_l[dir], y+y_l[dir], x+x_r[dir], y+y_r[dir]))
        {
            do img.draw(off_x, off_y, dir);
            return false;
        }
        else{
            do map.update(x+x_l[dir], y+y_l[dir], x+x_r[dir], y+y_r[dir], type);
            if(dir=0)
            {
                let y = y-1;
                let off_y = off_y-8;
            }
            else{if(dir=1)
            {
                let x = x+1;
                let off_x = off_x+8;
            }
            else{if(dir=2)
            {
                let y = y+1;
                let off_y = off_y+8;
            }
            else{if(dir=3)
            {
                let x = x-1;
                let off_x = off_x-8;
            }}}}
            do img.draw(off_x, off_y, dir);
            do map.update(x+x_l[op[dir]], y+y_l[op[dir]], x+x_r[op[dir]], y+y_r[op[dir]], 0);
        }
        return true;
    }
	method int get_type()
    {
		return type;
	}
	method int get_live(){
		return live;
	}
    method boolean dead()
    {
        return (live = 0);
    }
    method int attacked(int _x, int _y)
    {
        if((live>0)&(_x>(x-1))&(_x<(x+3))&(_y>(y-1))&(_y<(y+3)))
        {
            let live = live - 1;
            if(live = 0) { do _die(); return 2;}
			return 1;
        }
        return 0;
    }
    method void _die()
    {
        do map.update(x, y, x+2, y+2, 0);
		do Game.add_bomb(Bomb.new(x, y));
        return;
    }
}
