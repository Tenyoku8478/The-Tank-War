class Menu
{
    field Array list;
    field int now;
    field int row, col, n, width;
    field List cheat_list;

    constructor Menu new(int _row, int _col, int _n, Array _list)
    {
        var int i;
        var String str;
        let row = _row;
        let col = _col;
        let n = _n;
        let now = 0;
        let list = _list;
        let cheat_list = List.new();

        let i = 0;
        let width = 0;
        while(i<n)
        {
            let str = list[i];
            if(str[0] > width) { let width = str[0]; }
            let i = i+1;
        }
        return this;
    }
    method void _draw_select(boolean color)
    {
        do Screen.setColor(color);
        do Screen.drawLine(col*8-2, (row+now+now)*11-2, col*8-2, (row+now+now+1)*11+2);
        do Screen.drawLine((col+width)*8+1, (row+now+now)*11-2, (col+width)*8+1, (row+now+now+1)*11+2);
        do Screen.drawLine(col*8-2, (row+now+now)*11-2, (col+width)*8+1, (row+now+now)*11-2);
        do Screen.drawLine(col*8-2, (row+now+now+1)*11+2, (col+width)*8+1, (row+now+now+1)*11+2);
        return;
    }
    method char _input()
    {
        var char pre, in;
        let in = 0;
        while(in=0){let in = Keyboard.keyPressed();}
        while(~(in=0))
        {
            let pre = in;
            let in = Keyboard.keyPressed();
        }
        return pre;
    }
    method void _reset_cheat()
    {
        var CheatCode code;
        do cheat_list.reset();
        while(~cheat_list.end())
        {
            let code = cheat_list.get();
            do code.reset();
            do cheat_list.next();
        }
        return;
    }
    method int _check_cheat(char c)
    {
        var CheatCode code;
        var int i;
        let i = 0;
        do cheat_list.reset();
        while(~cheat_list.end())
        {
            let code = cheat_list.get();
            if(code.complete(c)) { return n+i; }
            let i = i+1;
            do cheat_list.next();
        }
        return -1;
    }

    method int run()
    {
        var int i;
        var char c;
        var int flag;
        let i = 0;
        while(i<n)
        {
            do Output.moveCursor(row+(i+i), col);
            do Output.printString(list[i]);
            let i = i+1;
        }
        do _reset_cheat();
        do _draw_select(1);
        while(true)
        {
            let c = _input();
            let flag = _check_cheat(c);
            if(~(flag=-1)) {return flag;}
            if((c = 131)&(now>0))
            {
                do _draw_select(0);
                let now = now-1;
                do _draw_select(1);
            }
            if((c = 133)&(now+1<n))
            {
                do _draw_select(0);
                let now = now+1;
                do _draw_select(1);
            }
            if(c = 32) //Space key
            {
                do _draw_select(0);
                do Screen.setColor(false);
                do Screen.drawRectangle(col*8, row*11, (col+width)*8-1, (row+n+n)*11-1);
                return now;
            }
            if(c = 81) //Q
            {
                do _draw_select(0);
                do Screen.setColor(false);
                do Screen.drawRectangle(col*8, row*11, (col+width)*8-1, (row+n+n)*11-1);
                return -1;
            }
        }
        return now;
    }
    method void add_cheat(String code)
    {
        do cheat_list.insert(CheatCode.new(code));
        return;
    }
}
